buildscript {
  repositories {
    maven { url = 'https://files.minecraftforge.net/maven' }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
  }
}

apply plugin: 'eclipse'

allprojects {
  apply plugin: 'net.minecraftforge.gradle'

  sourceCompatibility = targetCompatibility = '1.8'

  minecraft {
    mappings channel: 'snapshot', version: '20180921-1.13'
  }

  afterEvaluate {
    minecraft.runs.values().each {
      it.workingDirectory project.file('minecraft').canonicalPath
    }
  }

  dependencies {
    minecraft 'net.minecraftforge:forge:1.13.2-25.0.10'
  }
}

subprojects {
  dependencies {
    implementation project(':')
  }

  jar {
    manifest {
      attributes([
          'Specification-Title'     : 'llibrary',
          'Specification-Vendor'    : 'gegy1000',
          'Specification-Version'   : '24.0',
          'Implementation-Title'    : project.name,
          'Implementation-Version'  : "${version}",
          'Implementation-Vendor'   : 'gegy1000',
          'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
      ],)
    }
  }
}
